plugins {
    // --- Kotlin & Spring Boot setup ---
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.25'

    // --- Spring Boot & Dependency Management ---
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'

    // --- Publishing (for pushing to repositories) ---
    id 'maven-publish'
	id 'org.jlleitschuh.gradle.ktlint' version '12.1.0'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()

    // --- GitHub Packages (custom repository for printscript dependencies) ---
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/ingsis-team/printscript")
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

kotlin {
    // --- Kotlin JVM configuration ---
    jvmToolchain(21)
    targetCompatibility = JavaVersion.VERSION_21
}

dependencies {
    // --- Spring Boot Core ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // --- Swagger/OpenAPI Documentation ---
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

    // --- WebFlux & Reactive support ---
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions:1.1.5'

    // --- Redis (non-reactive) ---
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'redis.clients:jedis:3.7.0'

    // --- Kotlin & Coroutines ---
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactive:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.6.4'

    // --- JSON Serialization ---
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    // --- Database driver ---
    runtimeOnly 'org.postgresql:postgresql'

    // --- Lombok (for code generation) ---
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    // --- Printscript internal modules (from GitHub Packages) ---
    implementation 'printscript:commons:1.0.1-SNAPSHOT'
    implementation 'printscript:lexer:1.0.1-SNAPSHOT'
    implementation 'printscript:parser:1.0.1-SNAPSHOT'
    implementation 'printscript:linter:1.0.1-SNAPSHOT'
    implementation 'printscript:formatter:1.0.1-SNAPSHOT'
    implementation 'printscript:interpreter:1.0.1-SNAPSHOT'

    // --- Testing ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.2.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {

    useJUnitPlatform()

	finalizedBy('jacocoTestReport') // Ensure JaCoCo report is generated after tests

}



ktlint {
	disabledRules.set(["standard:package-name"])
	filter {
		exclude("**/generated/**")
	}
	version = "0.50.0"
	reporters {
		reporter "checkstyle"
		reporter "json"
	}
}



jacoco {

    toolVersion = "0.8.11"

}



tasks.jacocoTestReport {

    dependsOn(tasks.test)

    reports {

        xml.required.set(true)

        csv.required.set(false)

        html.required.set(true)

    }

	classDirectories.setFrom(

		files(

			fileTree(

				dir: "${buildDir}/classes/kotlin/main",

				excludes: [

					'**/*Application*',

					'**/*DTO*',

					'**/*Test*',

					'**/*Config*',

					'**/*Connector*',

					'**/*Controller*',

					'**/*Exception*',

					'**/*Repository*',

					'**/*Security*',

					'**/*Enum*',

					'**/*Model*',

					'**/*Entity*'

				]

			)

		)

	)

}



tasks.jacocoTestCoverageVerification {

    dependsOn(tasks.jacocoTestReport)

    violationRules {

        rule {

            limit {

                minimum = "0.80".toBigDecimal()

            }

        }

    }

	classDirectories.setFrom(

		files(

			fileTree(

				dir: "${buildDir}/classes/kotlin/main",

				excludes: [

					'**/*Application*',

					'**/*DTO*',

					'**/*Test*',

					'**/*Config*',

					'**/*Connector*',

					'**/*Controller*',

					'**/*Exception*',

					'**/*Repository*',

					'**/*Security*',

					'**/*Service*',

					'**/*Util*',

					'**/*Enum*',

					'**/*Model*',

					'**/*Entity*'

				]

			)

		)

	)

}



tasks.check {

    dependsOn(tasks.jacocoTestCoverageVerification)

}
